varnishtest "Test that hash_always_miss also implies hash_ignore_busy. Ticket #1073."

server s1 {
	rxreq 
	sema r1 sync 2
	sema r2 sync 2
	delay 1
	txresp -hdr "Server: 1"
} -start

server s2 {
	rxreq
	sema r2 sync 2
	txresp -hdr "Server: 2"
} -start

varnish v1 -vcl+backend { 
	sub vcl_recv {
		if (req.http.x-hash-always-miss == "1") {
			set req.hash_always_miss = true;
		}
		if (req.http.x-client == "1") {
			set req.backend = s1;
		}
		if (req.http.x-client == "2") {
			set req.backend = s2;
		}
	}
	sub vcl_deliver {
		if(obj.hits > 0) {
			set resp.http.X-Cache = "HIT";
		} else {
			set resp.http.X-Cache = "MISS";
		}
	}
} -start 

client c1 {
	txreq -url "/" -hdr "x-client: 1"
	rxresp
	expect resp.status == 200
	expect resp.http.Server == "1"
} -start

client c2 {
    sema r1 sync 2
	txreq -url "/" -hdr "x-client: 2" -hdr "x-hash-always-miss: 1"
	txreq -url "/" -hdr "x-client: 2"
	rxresp
	expect resp.status == 200
	expect resp.http.Server == "2"
} -start

client c1 -wait
client c2 -wait
