varnishtest "Test fallback director"

server s1 {
        rxreq
        txresp -hdr "Foo: 1"
} -start

server s2 {
        rxreq
        txresp -hdr "Foo: 2"
} -start

server s3 {
        rxreq
        txresp -hdr "Foo: 3"
} -start

varnish v1 -vcl {
        backend b1 {
                .host = "${s1_addr}";
                .port = "${s1_port}";
        }
        backend b2 {
                .host = "${s2_addr}";
                .port = "${s2_port}";
        }
        backend b3 {
                .host = "${s3_addr}";
                .port = "${s3_port}";
        }
        director f1 fallback {
                { .backend = b1; }
                { .backend = b2; }
                { .backend = b3; }
        }
        sub vcl_recv {
                set req.backend = f1;
                return (pass);
        }
} -start

varnish v1 -cliok "backend.set_health b1 sick"
varnish v1 -cliok "backend.set_health b2 sick"

client c1 {
        # s1 & s2 are both sick, expect response from s3
        txreq
        rxresp
        expect resp.http.foo == "3"
} -run

varnish v1 -cliok "backend.set_health b2 healthy"

client c1 {
        txreq
        rxresp
        expect resp.http.foo == "2"
} -run

varnish v1 -cliok "backend.set_health b1 healthy"

client c1 {
        txreq
        rxresp
        expect resp.http.foo == "1"
} -run
